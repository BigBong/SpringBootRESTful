apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'org.springframework.boot'

configurations {
    mybatisGenerator
}

dependencies {
    compile("org.springframework.boot:spring-boot-starter")
    compile("org.springframework.boot:spring-boot-starter-jdbc")

    // http://www.mybatis.org/spring-boot-starter/mybatis-spring-boot-autoconfigure/
    compile("org.mybatis.spring.boot:mybatis-spring-boot-starter:1.1.1")

    compile libraries.mybatis
    compile libraries.mybatis_spring
    compile libraries.mysql

    testCompile("org.springframework.boot:spring-boot-starter-test")

    mybatisGenerator libraries.mybatis_generator
    mybatisGenerator libraries.mysql
}

def getProperties(fileName) {
    def properties = new Properties()
    file("src/main/resources/" + fileName).withInputStream { inputStream ->
        properties.load(inputStream)
    }
    properties;
}

task mybatisGenerate << {
    def properties = getProperties("mybatis-generator.properties")

    def destDir = new File("src", 'main')

    ant.properties['targetProject'] = destDir.path
    ant.properties['mysqlConnectorJarPath'] = "libs/mysql-connector-java-5.1.39.jar"
    ant.properties['driverClass'] = properties.getProperty("jdbc.driverClassName")
    ant.properties['connectionURL'] = properties.getProperty("jdbc.url")
    ant.properties['userId'] = properties.getProperty("jdbc.user")
    ant.properties['password'] = properties.getProperty("jdbc.pass")
    ant.properties['src_main_java'] = sourceSets.main.java.srcDirs[0].path
    ant.properties['src_main_resources'] = sourceSets.main.resources.srcDirs[0].path
    ant.properties['modelPackage'] = properties.getProperty("generator.modelPackage")
    ant.properties['mapperPackage'] = properties.getProperty("generator.mapperPackage")
    ant.properties['sqlMapperPackage'] = properties.getProperty("generator.sqlMapperPackage")

    ant.taskdef(
            name: 'mbgenerator',
            classname: 'org.mybatis.generator.ant.GeneratorAntTask',
            classpath: configurations.mybatisGenerator.asPath
    )
    ant.mbgenerator(overwrite: true,
            configfile: 'src/main/resources/mybatis/generator-config.xml', verbose: true) {
        propertyset {
            propertyref(name: 'targetProject')
            propertyref(name: 'mysqlConnectorJarPath')
            propertyref(name: 'userId')
            propertyref(name: 'driverClass')
            propertyref(name: 'connectionURL')
            propertyref(name: 'password')
            propertyref(name: 'src_main_java')
            propertyref(name: 'src_main_resources')
            propertyref(name: 'modelPackage')
            propertyref(name: 'mapperPackage')
            propertyref(name: 'sqlMapperPackage')
        }
    }
}